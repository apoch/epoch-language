//
// The Epoch Language Project
// Epoch Development Tools - Common Library Modules
//
// STRINGS.EPOCH
// Common string processing and manipulation routines
//



ExtractLine : string ref contents -> string line = ""
{
	integer len = length(contents)
	
	if(len == 0)
	{
		return()
	}

	integer pos = 0
	while(charat(contents, pos) != unescape("\r"))
	{
		++pos
		
		if(pos == len)
		{
			line = substring(contents, 0, len)
			contents = ""
			return()
		}
	}

	line = substring(contents, 0, pos)
	
	pos += 2
	contents = substring(contents, pos)
}


ReadFile : string filename, integer ref len -> string contents = ""
{
	Win32Handle INVALID_HANDLE_VALUE = 0xffffffff
	integer FILE_SHARE_READ = 0x01
	integer OPEN_EXISTING = 3
	integer GENERIC_READ = 0x80000000

	Win32Handle filehandle = CreateFile(filename, GENERIC_READ, 0, 0, OPEN_EXISTING, 0, 0)
	if(filehandle != INVALID_HANDLE_VALUE)
	{
		integer PAGE_READONLY = 0x02

		len = GetFileSize(filehandle, 0)

		if(len > 0)
		{
			Win32Handle mappinghandle = CreateFileMapping(filehandle, 0, PAGE_READONLY, 0, 0, 0)
			if(mappinghandle != 0)
			{
				integer FILE_MAP_READ = 0x04
				integer ptr = MapViewOfFile(mappinghandle, FILE_MAP_READ, 0, 0, len)

				if(ptr != 0)
				{
					contents = widenfromptr(ptr, len)
					UnmapViewOfFile(ptr)
				}
				CloseHandle(mappinghandle)
			}
			else
			{
				print("Failed to map file!")
			}
		}
		else
		{
			contents = ""
		}
		CloseHandle(filehandle)
	}
	else
	{
		print("Couldn't open that file!")
	}
}



// TODO - replace with better option
charat : string in, integer index -> string c = substring(in, index, 1)



QuoteString : string in -> string out = unescape("\'") ; in ; unescape("\'")


stringcontains : string haystack, string needle -> boolean contains = false
{
	integer needlelen = length(needle)
	integer haystacklen = length(haystack)

	integer index = 0
	while(index < (haystacklen - needlelen))
	{
		if(substring(haystack, index, needlelen) == needle)
		{
			contains = true
			return()
		}

		++index
	}
}


stringsplit : string haystack, simplelist<string> ref outlist, string delimiterchar -> integer chunks = 0
{
	while(stringcontains(haystack, delimiterchar))
	{
		integer i = 0
		while(i < length(haystack))
		{
			string c = charat(haystack, i)
			if(c == delimiterchar)
			{
				string chunk = substring(haystack, 0, i)
				haystack = substring(haystack, i + 1)
				
				++chunks
				//simple_append<string>(outlist, chunk)
				
				i = 0
			}
			else
			{
				++i
			}
		}
	}

	if(length(haystack) > 0)
	{
		++chunks
		//simple_append<string>(outlist, haystack)
	}
}

