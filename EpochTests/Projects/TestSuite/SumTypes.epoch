//
// SUMTYPES.EPOCH
//
// Test suite for sum types
//


TestSumTypes : Harness ref harness
{
	TestSection(harness, "Sum types")
	
	TSTPassthrough(harness)
	TSTTypeMatch(harness)
	TSTNothing(harness)

	TestSectionComplete(harness)
}


type BasicSumType : integer | string


TSTPassthrough : Harness ref harness
{
	BasicSumType test = 42
	TSTCheckPassthrough(harness, test)
}

TSTCheckPassthrough : Harness ref harness, BasicSumType ref test
{
	TestAssert(true, harness, "basic sumtype parameter passthrough")
}



TSTTypeMatch : Harness ref harness
{
	Harness foo = 0, 0, 0, 0

	BasicSumType test = 42
	TSTCheckTypeMatch(foo, test)
	
	BasicSumType test2 = "test"
	TSTCheckTypeMatch(foo, test2)
}

TSTCheckTypeMatch : Harness ref harness, integer val
{
	TestAssert(val == 42, harness, "sumtype type matching - integer fork")
}

TSTCheckTypeMatch : Harness ref harness, string val
{
	TestAssert(val == "test", harness, "sumtype type matching - string fork")
}



type OptionalStringTest : string | nothing

TSTNothing : Harness ref harness
{
	OptionalStringTest alpha = "testing"
	OptionalStringTest beta = nothing

	TSTCheckNothing(alpha, false, harness)
	TSTCheckNothing(beta, true, harness)
}


TSTCheckNothing : string s, boolean wantNothing, Harness ref harness
{
	TestAssert(!wantNothing, harness, "sumtype type matching - expectation vs reality - string")
}

TSTCheckNothing : nothing, boolean wantNothing, Harness ref harness
{
	TestAssert(wantNothing, harness, "sumtype type matching - expectation vs reality - nothing")
}