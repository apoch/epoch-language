//
// EpochCompiler
// Project Entry Point Module
//
// Apoch - 8/26/2018 10:27:02 AM
//

entrypoint :
{
	print("Epoch Language Project")
	print("Cross-compiler: 32-bit compiler that builds 64-bit outputs")
	print("")

	string files = ""
	string output = ""
	
	integer cmdlineindex = 1
	while(cmdlineindex < cmdlinegetcount())
	{
		string switch = cmdlineget(cmdlineindex)
		
		if(switch == "/files")
		{
			++cmdlineindex
			files = cmdlineget(cmdlineindex)
		}
		elseif(switch == "/output")
		{
			++cmdlineindex
			output = cmdlineget(cmdlineindex)
		}
		
		++cmdlineindex
	}
	
	if(length(files) == 0)
	{
		print("No input files specified; use /files switch")
		AbortProcess(100)
	}
	
	if(length(output) == 0)
	{
		output = "EpochProgram.exe"
	}


	ListValueNode<string> sourcefilelist = nothing

	string split = files
	integer i = 0
	while(i < length(split))
	{
		string c = charat(split, i)
		if(c == ";")
		{
			string singlefile = substring(split, 0, i)
			split = substring(split, i + 1)

			ListAppendV<string>(sourcefilelist, singlefile)
				
			i = 0
		}
		else
		{
			++i
		}
	}
	
	if(length(split) > 0)
	{
		ListAppendV<string>(sourcefilelist, split)
	}
	
	print("Compilation arguments:")
	DumpList<string>(sourcefilelist)

	print(" --->")
	print(output)

	print("")

	StringPool tokenpool = BinaryTreeRoot<string>(nothing), Trie(0, nothing, 0), INVALID_STRING_HANDLE 
	StringPool literalpool = BinaryTreeRoot<string>(nothing), Trie(0, nothing, 0), INVALID_STRING_HANDLE 
	Namespace rootnamespace = INVALID_STRING_HANDLE, nothing
	Program program = rootnamespace, nothing, tokenpool, literalpool

	if(!ParseFiles(sourcefilelist, program))
	{
		print("*** ERROR: Failed to parse input files.")
		AbortProcess(200)
	}


	// TODO - type checking (error code 300 is reserved for this phase)


	LLVMContextHandle context = EpochLLVMContextCreate()
	if(!CodeGenProgram(program, context))
	{
		print("*** ERROR: Failed to code-gen program.")
		EpochLLVMContextDestroy(context)
		AbortProcess(400)
	}

	if(!LinkAndWriteProgram(program, context, output))
	{
		print("*** ERROR: Failed to link program.")
		EpochLLVMContextDestroy(context)
		AbortProcess(500)
	}
	EpochLLVMContextDestroy(context)

	print("Completed successfully.")
}

