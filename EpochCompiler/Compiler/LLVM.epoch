//
// LLVM integration
//


type LLVMContextHandle : integer
type LLVMFunctionType : integer
type LLVMFunction : integer
type LLVMFunctionThunk : integer
type LLVMBasicBlock : integer
type LLVMBuffer : integer
type LLVMValue : integer
type LLVMType : integer

EpochLLVMContextCreate : -> LLVMContextHandle ret = 0 																		[external("EpochLLVM.dll", "EpochLLVMContextCreate")]
EpochLLVMContextDestroy : LLVMContextHandle context																			[external("EpochLLVM.dll", "EpochLLVMContextDestroy")]

EpochLLVMContextSetStringPoolCallback : LLVMContextHandle context, (func : integer, integer -> integer), integer void		[external("EpochLLVM.dll", "EpochLLVMContextSetStringPoolCallback")]

EpochLLVMModuleDump : LLVMContextHandle context																				[external("EpochLLVM.dll", "EpochLLVMModuleDump")]
EpochLLVMModuleCreateBinary : LLVMContextHandle context																		[external("EpochLLVM.dll", "EpochLLVMModuleCreateBinary")]
EpochLLVMModuleFinalize : LLVMContextHandle context, integer baseAddress, integer codeOffset								[external("EpochLLVM.dll", "EpochLLVMModuleFinalize")]
EpochLLVMModuleGetCodeBuffer : LLVMContextHandle context, integer ref size -> LLVMBuffer ret = 0							[external("EpochLLVM.dll", "EpochLLVMModuleGetCodeBuffer")]
EpochLLVMModuleGetPDataBuffer : LLVMContextHandle context, integer ref size -> LLVMBuffer ret = 0							[external("EpochLLVM.dll", "EpochLLVMModuleGetPDataBuffer")]
EpochLLVMModuleGetXDataBuffer : LLVMContextHandle context, integer ref size -> LLVMBuffer ret = 0							[external("EpochLLVM.dll", "EpochLLVMModuleGetXDataBuffer")]
EpochLLVMModuleRelocateBuffers : LLVMContextHandle context, integer codeOffset, integer xDataOffset							[external("EpochLLVM.dll", "EpochLLVMModuleRelocateBuffers")]

EpochLLVMTypeCreateFunction : LLVMContextHandle context -> LLVMFunctionType ret = 0											[external("EpochLLVM.dll", "EpochLLVMTypeCreateFunction")]
EpochLLVMTypeQueueFunctionParameter : LLVMContextHandle context, LLVMType ty												[external("EpochLLVM.dll", "EpochLLVMTypeQueueFunctionParameter")]
EpochLLVMTypeGetString : LLVMContextHandle context -> LLVMType ty = 0														[external("EpochLLVM.dll", "EpochLLVMTypeGetString")]

EpochLLVMFunctionCreate : LLVMContextHandle context, LLVMFunctionType fty, string name  -> LLVMFunction ret = 0				[external("EpochLLVM.dll", "EpochLLVMFunctionCreate")]
EpochLLVMFunctionCreateThunk : LLVMContextHandle context, LLVMFunctionType fty, string name  -> LLVMFunctionThunk ret = 0	[external("EpochLLVM.dll", "EpochLLVMFunctionCreateThunk")]

EpochLLVMBasicBlockCreate : LLVMContextHandle context, LLVMFunction func -> LLVMBasicBlock ret = 0							[external("EpochLLVM.dll", "EpochLLVMBasicBlockCreate")]

EpochLLVMBasicBlockSetInsertPoint : LLVMContextHandle context, LLVMBasicBlock block											[external("EpochLLVM.dll", "EpochLLVMBasicBlockSetInsertPoint")]


EpochLLVMCodeCreateRetVoid : LLVMContextHandle context																		[external("EpochLLVM.dll", "EpochLLVMCodeCreateRetVoid")]
EpochLLVMCodeCreateCall : LLVMContextHandle context, LLVMFunction target -> LLVMValue ret = 0								[external("EpochLLVM.dll", "EpochLLVMCodeCreateCall")]
EpochLLVMCodeCreateCallThunk : LLVMContextHandle context, LLVMFunctionThunk target -> LLVMValue ret = 0						[external("EpochLLVM.dll", "EpochLLVMCodeCreateCallThunk")]

EpochLLVMCodePushValue : LLVMContextHandle context, LLVMValue value															[external("EpochLLVM.dll", "EpochLLVMCodePushValue")]

EpochLLVMCodeGetStringValue : LLVMContextHandle context, integer index -> LLVMValue value = 0								[external("EpochLLVM.dll", "EpochLLVMCodeGetStringValue")]

EpochLLVMModuleGetDebugBuffer : LLVMContextHandle context, integer ref size -> LLVMBuffer ret = 0							[external("EpochLLVM.dll", "EpochLLVMModuleGetDebugBuffer")]
EpochLLVMModuleGetDebugRelocBuffer : LLVMContextHandle context, integer ref size -> LLVMBuffer ret = 0						[external("EpochLLVM.dll", "EpochLLVMModuleGetDebugRelocBuffer")]
EpochLLVMModuleGetDebugSymbolsBuffer : LLVMContextHandle context, integer ref size, integer ref count -> LLVMBuffer ret = 0	[external("EpochLLVM.dll", "EpochLLVMModuleGetDebugSymbolsBuffer")]



structure CodeGenCache :
	Program ref CodeGenProgram,
	LLVMContextHandle LLVM,
	LLVMFunction EntrypointFunction,
	LLVMFunctionThunk PrintThunk



CodeGenProgram : Program ref program, LLVMContextHandle context -> boolean success = false
{
	EpochLLVMContextSetStringPoolCallback(context, StringLookupMapper, 0)

	print("Generating code...")

	EpochLLVMTypeQueueFunctionParameter(context, EpochLLVMTypeGetString(context))
	LLVMFunctionType thunkType = EpochLLVMTypeCreateFunction(context)
	LLVMFunctionThunk thunk = EpochLLVMFunctionCreateThunk(context, thunkType, "print")

	LLVMFunctionType initFuncType = EpochLLVMTypeCreateFunction(context)
	LLVMFunction initFunc = EpochLLVMFunctionCreate(context, initFuncType, "@init")
	LLVMBasicBlock initBB = EpochLLVMBasicBlockCreate(context, initFunc)

	LLVMFunction ep = 0
	CodeGenCache cache = program, context, ep, thunk

	if(!CodeGenNamespace(program.RootNamespace, cache))
	{
		return()
	}

	// TODO - validate that entrypoint exists

	EpochLLVMBasicBlockSetInsertPoint(context, initBB)
	EpochLLVMCodeCreateCall(context, cache.EntrypointFunction)
	EpochLLVMCodeCreateRetVoid(context)

	EpochLLVMModuleCreateBinary(context)
	success = true
}



CodeGenNamespace : Namespace ref namespace, CodeGenCache ref cache -> boolean success = false
{
	if(!CodeGenFunctions(namespace.Functions, cache))
	{
		return()
	}

	success = true
}


CodeGenFunctions : ListRef<Function> ref functions, CodeGenCache ref cache -> boolean success = false
{
	if(!CodeGenFunction(functions.Head, cache))
	{
		return()
	}

	if(CodeGenFunctions(functions.Next, cache))
	{
		success = true
	}
}

CodeGenFunctions : nothing, CodeGenCache ref cache -> true



CodeGenFunction : Function ref function, CodeGenCache ref cache -> boolean success = false
{
	LLVMFunctionType fty = EpochLLVMTypeCreateFunction(cache.LLVM)
	LLVMFunction llvmfunc = EpochLLVMFunctionCreate(cache.LLVM, fty, GetPooledString(cache.CodeGenProgram.TokenStringPool, function.Name))

	LLVMBasicBlock bb = EpochLLVMBasicBlockCreate(cache.LLVM, llvmfunc)
	EpochLLVMBasicBlockSetInsertPoint(cache.LLVM, bb)
	
	if(!CodeGenCodeBlock(function.Code, cache))
	{
		return()
	}

	if(GetPooledString(cache.CodeGenProgram.TokenStringPool, function.Name) == "entrypoint")
	{
		cache.EntrypointFunction = llvmfunc
	}
	
	EpochLLVMCodeCreateRetVoid(cache.LLVM)
	success = true
}



CodeGenCodeBlock : CodeBlock ref block, CodeGenCache ref cache -> boolean success = CodeGenCodeBlockEntries(block.Entries, cache)

CodeGenCodeBlock : nothing, CodeGenCache ref cache -> true



CodeGenCodeBlockEntries : ListRef<CodeBlockEntry> ref entries, CodeGenCache ref cache -> boolean success = false
{
	if(!CodeGenCodeBlockEntry(entries.Head, cache))
	{
		return()
	}

	success = CodeGenCodeBlockEntries(entries.Next, cache)
}

CodeGenCodeBlockEntries : nothing, CodeGenCache ref cache -> true



CodeGenCodeBlockEntry : Statement ref statement, CodeGenCache ref cache -> boolean success = false
{
	// TODO - this is hacky but it'll work for now
	if(GetPooledString(cache.CodeGenProgram.TokenStringPool, statement.Name) != "print")
	{
		return()
	}

	if(!CodeGenExpressionList(statement.Arguments, cache))
	{
		return()
	}

	EpochLLVMCodeCreateCallThunk(cache.LLVM, cache.PrintThunk)

	success = true
}


CodeGenExpressionList : ListRef<Expression> ref expressions, CodeGenCache ref cache -> boolean success = false
{
	if(!CodeGenExpression(expressions.Head, cache))
	{
		return()
	}

	success = CodeGenExpressionList(expressions.Next, cache)
}

CodeGenExpressionList : nothing, CodeGenCache ref cache -> true



CodeGenExpression : Expression ref expression, CodeGenCache ref cache -> boolean success = false
{
	success = CodeGenExpressionAtoms(expression.Atoms, cache)
}

CodeGenExpressionAtoms : ListRef<ExpressionAtom> ref atoms, CodeGenCache ref cache -> boolean success = false
{
	if(!CodeGenExpressionAtom(atoms.Head, cache))
	{
		return()
	}

	success = CodeGenExpressionAtoms(atoms.Next, cache)
}

CodeGenExpressionAtoms : nothing, CodeGenCache ref cache -> true


CodeGenExpressionAtom : StringAtom ref atom, CodeGenCache ref cache -> true
{
	LLVMValue strval = EpochLLVMCodeGetStringValue(cache.LLVM, atom.String)
	EpochLLVMCodePushValue(cache.LLVM, strval)
}
